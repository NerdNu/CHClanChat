
*:/clanchat = >>>
	msg(color(RED).'ClanChat usage:')
	msg(color(BLUE).'/clanchat create <channel>'.color(WHITE).' - Creates a new channel, with you as the owner.')
	msg(color(BLUE).'/clanchat color <channel> <color>'.color(WHITE).' - Sets the channel color. Only the owner can set this.')
	msg(color(BLUE).'/clanchat members <channel>'.color(WHITE).' - Lists all the members in a channel.')
	msg(color(BLUE).'/clanchat invite <channel> <player>'.color(WHITE).' - Invites a player to the channel. You must be a manager to invite.')
	msg(color(BLUE).'/clanchat uninvite <channel> <player>'.color(WHITE).' - Uninvites a previously invited player. You must be a manager.')
	msg(color(BLUE).'/clanchat changeowner <channel> <player>'.color(WHITE).' - Changes the owner on the given channel. Only the owner can set this. '.color(RED).'Careful! This can\'t be undone.')
	msg(color(BLUE).'/clanchat addmanager <channel> <player>'.color(WHITE).' - Adds a manager to the channel. You must be the owner to run this.')
	msg(color(BLUE).'/clanchat removemanager <channel> <player>'.color(WHITE).' - Removes a manager from the channel. You must be the owner to run this.')
	msg(color(BLUE).'/clanchat remove <channel> <player>'.color(WHITE).' - Removes a player from the channel. You must be a manager to do this.')
	msg(color(BLUE).'/clanchat join <channel>'.color(WHITE).' - Joins a channel that you\'ve already been invited to.')
	msg(color(BLUE).'/clanchat leave <channel>'.color(WHITE).' - Leaves a channel that you\'re in.')
	msg(color(BLUE).'/clanchat list'.color(WHITE).' - Lists all the channels you are in.')
	msg(color(BLUE).'/c [#<channel>] <message...>'.color(WHITE).' - Sends a message to the channel. You must be a member of the channel. If you include the'
			.' channel name (prefixed with #) it will select a channel. After you select a channel, it becomes the default channel, and you no longer'
			.' need to provide the channel name, unless you want to switch default channels.')
	msg(color(BLUE).'/cr <message...>'.color(WHITE).' - Sends a message to the last channel that you received a message from, regardless of what your default'
			.' channel is.')
	msg(color(BLUE).'/cm'.color(WHITE).' - Lists all the members in the last channel you recieved a message in.')
	if(has_permission('nerdch.level.admin')){
		msg(color(BLUE).'/clanchat channels'.color(WHITE).' - Lists all the channels and their owners.')
	}
	if(player() == '~console'){
		msg(color(BLUE).'/clanchat chat <channel> <message>'.color(WHITE).' - Chats to an arbitrary channel. Only available to console.')
	}
<<<

*:/clanchat create $channelName = >>>
	_cc_create_channel($channelName)
<<<

*:/clanchat color $channel $color = >>>
	_cc_set_channel_color($channel, $color)	
<<<

*:/clanchat members $channelName = >>>
	_cc_print_members($channelName)
<<<

*:/clanchat invite $channel $player = >>>
	_cc_invite_member($channel, $player)
<<<

*:/clanchat uninvite $channel $player = >>>
	_cc_uninvite_member($channel, $player)
<<<

*:/clanchat changeOwner $channel $player = >>>
	_cc_change_owner($channel, $player)
<<<

*:/clanchat addmanager $channel $player = >>>
	_cc_add_manager($channel, $player)
<<<

*:/clanchat removemanager $channel $player = >>>
	_cc_remove_manager($channel, $player)
<<<

*:/clanchat remove $channel $player = >>>
	_cc_remove_member($channel, $player)
<<<

*:/clanchat join $channel = >>>
	_cc_join_channel($channel)
<<<

*:/clanchat leave $channel = >>>
	_cc_leave_channel($channel)
<<<

*:/clanchat list = >>>
	_cc_print_channels()
<<<

*:/clanchat channels = >>>
	_assertperm('admin')
	_cc_print_all_channels()	
<<<

*:/clanchat chat $channel $ = >>>
	if(player() != '~console'){
		die('Sorry, you can\'t use this command')
	}
	_cc_send_message($channel, $)
<<<

*:/c [$] = >>>
	if($ == ''){
		die(color(RED).'Speak up some!')
	}
	@args = parse_args($)
	@channelMatch = reg_match('^(?i)\\s*#([a-z0-9_]+)\\s*$', @args[0])
	@channel = null
	@message = null
	if(array_size(@channelMatch) > 1){
		@channel = @channelMatch[1]
		@message = array_implode(@args[1..], ' ')
	} else {
		@channel = _cc_get_default()
		@message = $
	}
	if(@channel == null){
		die(color(RED).'You don\'t have a default channel set (or aren\'t in any channels). Run '.color(LIGHT_PURPLE).'/clanchat'.color(RED).' for help')
	}
	_cc_send_message(@channel, @message)
<<<

*:/cr [$] = >>>
	@channel = _cc_get_last_channel_received(strip_colors(player()))
	if(@channel == null){
		die(color(RED).'No previous channel to send to')
	} else {
		_cc_send_message(@channel, $)
	}
<<<

*:/cm [$] = >>>
	@channel = _cc_get_last_channel_recieved(strip_colors(player()))
	if (@channel == null){
		die(color(RED).'No previous channel')
	} else {
		_cc_print_members(@channel)
	}
<<<
